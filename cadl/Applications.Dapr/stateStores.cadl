/*
Copyright 2023 The Radius Authors.

Licensed under the Apache License, Version 2.0 (the "License");
you may not use this file except in compliance with the License.
You may obtain a copy of the License at

    http://www.apache.org/licenses/LICENSE-2.0
    
Unless required by applicable law or agreed to in writing, software
distributed under the License is distributed on an "AS IS" BASIS,
WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
See the License for the specific language governing permissions and
limitations under the License.
*/

import "@cadl-lang/rest";
import "@cadl-lang/versioning";
import "@azure-tools/cadl-azure-core";
import "@azure-tools/cadl-azure-resource-manager";
import "@azure-tools/cadl-providerhub";
import "@cadl-lang/openapi";

// These files allow the use of {rootScope} in the generated paths with a custom resourceOperations object
import "../customRootScope.cadl";
import "../aksrootscope.cadl";

using Cadl.Http;
using Cadl.Rest;
using Cadl.Versioning;
using Azure.ResourceManager;
using Azure.ResourceManager.Radius;
using OpenAPI;

namespace Applications.Dapr;

@doc("Dapr StateStore Properties for Mode Resource")
model ResourceDaprStateStoreProperties extends DaprStateStoreProperties {
  @doc("How to build the Dapr StateStore link. Options are to build automatically via 'recipe' or 'resource', or build manually via 'values'. Selection determines which set of fields to additionally require.")
  mode: "resource";

  @doc("Metadata for the state store resource. This should match the values specified in Dapr component spec")
  metadata?: object;

  @doc("Dapr StateStore type. These strings match the format used by Dapr Kubernetes configuration format.")
  type?: string;

  @doc("Dapr component version")
  version?: string;

  @doc("The resource id of the Azure SQL Database or Azure Table Storage the Dapr StateStore resource is connected to.")
  resource: string;

}
@doc("Dapr StateStore Properties for Mode Values")
model ValuesDaprStateStoreProperties extends DaprStateStoreProperties {
  @doc("How to build the Dapr StateStore link. Options are to build automatically via 'recipe' or 'resource', or build manually via 'values'. Selection determines which set of fields to additionally require.")
  mode: "values";

  @doc("Metadata for the state store resource. This should match the values specified in Dapr component spec")
  metadata: object;

  @doc("Dapr StateStore type. These strings match the format used by Dapr Kubernetes configuration format.")
  type: string;

  @doc("Dapr component version")
  version: string;
}
@doc("Dapr StateStore Properties for Mode Recipe")
model RecipeDaprStateStoreProperties extends DaprStateStoreProperties {
  @doc("How to build the Dapr StateStore link. Options are to build automatically via 'recipe' or 'resource', or build manually via 'values'. Selection determines which set of fields to additionally require.")
  mode: "recipe";

  @doc("Metadata for the state store resource. This should match the values specified in Dapr component spec")
  metadata?: object;

  @doc("Dapr StateStore type. These strings match the format used by Dapr Kubernetes configuration format.")
  type?: string;

  @doc("Dapr component version")
  version?: string;

  @doc("The recipe used to automatically deploy underlying infrastructure for the Dapr StateStore link")
  recipe: Recipe;
}
@doc("Dapr StateStore link properties")
@discriminator("mode")
model DaprStateStoreProperties extends BasicDaprResourceProperties {
  @doc("Provisioning state of the Dapr StateStore link at the time the operation was called")
  @visibility("read")
  provisioningState?: ProvisioningState;
}
@doc("Dapr StateStore link")
model DaprStateStoreResource is TrackedResource<DaprStateStoreProperties> {
  @doc("Dapr StateStore name")
  @maxLength(63)
  @pattern("^[A-Za-z]([-A-Za-z0-9\\.]*[A-Za-z0-9\\.])?$")
  @key("daprStateStoreName")
  @path
  @segment("daprStateStores")
  @extension("x-ms-parameter-location", "method")
  name: string;
}

@armResourceOperations
interface DaprStateStore
  extends Radius.ResourceOperationWithAsyncDelete<
      DaprStateStoreResource,
      DaprStateStoreProperties,
      RootScopeParam
    > {}
