/*
Copyright 2023 The Radius Authors.
Licensed under the Apache License, Version 2.0 (the "License");
you may not use this file except in compliance with the License.
You may obtain a copy of the License at
    http://www.apache.org/licenses/LICENSE-2.0
Unless required by applicable law or agreed to in writing, software
distributed under the License is distributed on an "AS IS" BASIS,
WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
See the License for the specific language governing permissions and
limitations under the License.
*/

import "@cadl-lang/rest";
import "@cadl-lang/versioning";
import "@azure-tools/cadl-azure-core";
import "@azure-tools/cadl-azure-resource-manager";
import "@azure-tools/cadl-providerhub";
import "@cadl-lang/openapi";

// These files allow the use of {rootScope} in the generated paths with a custom resourceOperations object
import "../customRootScope.cadl";
import "../aksrootscope.cadl";

using Cadl.Http;
using Cadl.Rest;
using Cadl.Versioning;
using Azure.ResourceManager;
using Azure.ResourceManager.Radius;
using OpenAPI;

namespace Applications.Messaging;

#suppress "@azure-tools/cadl-azure-core/casing-style" "The names of Model types must use PascalCase"
@doc("RabbitMQQueue portable resource")
model RabbitMQQueueResource
  is TrackedResource<RabbitMQQueueProperties> {
  @doc("The name of the RabbitMQQueue portable resource")
  @maxLength(63)
  @pattern("^[A-Za-z]([-A-Za-z0-9]*[A-Za-z0-9])?$")
  @key("rabbitMQQueueName")
  @path
  @segment("rabbitMQQueues")
  name: string;
}

#suppress "@azure-tools/cadl-azure-core/casing-style" "The names of Model types must use PascalCase"
@doc("The secret values for the given RabbitMQQueue resource")
model RabbitMQListSecretsResult is RabbitMQSecrets;

#suppress "@azure-tools/cadl-azure-core/casing-style" "The names of Model types must use PascalCase"
@doc("The secret values for the given RabbitMQQueue resource")
model RabbitMQSecrets {
  @doc("The connection string used to connect to this RabbitMQ instance")
  connectionString?: string;
}
#suppress "@azure-tools/cadl-azure-core/casing-style" "The names of Model types must use PascalCase"
@doc("RabbitMQQueue Properties for Mode Values")
model ValuesRabbitMQQueueProperties extends RabbitMQQueueProperties {
  @doc("How to build the RabbitMQQueue portable resource. Options are to build automatically via 'recipe' or 'resource', or build manually via 'values'. Selection determines which set of fields to additionally require.")
  mode: "values";

  @doc("The name of the queue")
  queue: string;
}
#suppress "@azure-tools/cadl-azure-core/casing-style" "The names of Model types must use PascalCase"
@doc("RabbitMQQueue Properties for Mode Recipe")
model RecipeRabbitMQQueueProperties extends RabbitMQQueueProperties {
  @doc("How to build the RabbitMQQueue portable resource. Options are to build automatically via 'recipe' or 'resource', or build manually via 'values'. Selection determines which set of fields to additionally require.")
  mode: "recipe";

  @doc("The name of the queue")
  queue?: string;

  @doc("The recipe used to automatically deploy underlying infrastructure for the rabbitMQQueue portable resource")
  recipe: Recipe;
}

#suppress "@azure-tools/cadl-azure-core/casing-style" "The names of Model types must use PascalCase"
@doc("RabbitMQQueue portable resource properties")
@discriminator("mode")
model RabbitMQQueueProperties extends BasicResourceProperties {
  @doc("Provisioning state of the rabbitMQ message queue portable resource at the time the operation was called")
  @visibility("read")
  provisioningState?: ProvisioningState;

  @doc("Secrets provided by resources,")
  secrets?: RabbitMQSecrets;
}

#suppress "@azure-tools/cadl-azure-core/casing-style" "The names of Model types must use camelCase"
#suppress "@azure-tools/cadl-azure-resource-manager/arm-resource-operation-outside-interface" "This is an interface template"
@doc("Lists secrets values for the specified RabbitMQQueue resource")
@post
@route("/{rootScope}/providers/Applications.Messaging/rabbitMQQueues/{rabbitMQQueueName}/listSecrets")
op RabbitMQQueues_listSecrets(
  ...ApiVersionParameter,
  ...RootScopeParam,
  @doc("The name of the RabbitMQQueue portable resource")
  @maxLength(63)
  @pattern("^[A-Za-z]([-A-Za-z0-9]*[A-Za-z0-9])?$")
  @path
  @extension("x-ms-parameter-location", "method")
  rabbitMQQueueName: string
): ArmResponse<RabbitMQListSecretsResult> | ErrorResponse;

#suppress "@azure-tools/cadl-azure-core/casing-style" "The names of Interface types must use PascalCase"
@armResourceOperations
interface RabbitMQQueues
  extends Radius.ResourceOperationWithSyncDelete<
      RabbitMQQueueResource,
      RabbitMQQueueProperties,
      RootScopeParam
    > {}
