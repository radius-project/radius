/*
Copyright 2023 The Radius Authors.

Licensed under the Apache License, Version 2.0 (the "License");
you may not use this file except in compliance with the License.
You may obtain a copy of the License at

    http://www.apache.org/licenses/LICENSE-2.0
    
Unless required by applicable law or agreed to in writing, software
distributed under the License is distributed on an "AS IS" BASIS,
WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
See the License for the specific language governing permissions and
limitations under the License.
*/

import "@cadl-lang/rest";
import "@cadl-lang/versioning";
import "@azure-tools/cadl-azure-core";
import "@azure-tools/cadl-azure-resource-manager";
import "@azure-tools/cadl-providerhub";
import "@cadl-lang/openapi";

// These files allow the use of {rootScope} in the generated paths with a custom resourceOperations object
import "../customRootScope.cadl";
import "../aksrootscope.cadl";

using Cadl.Http;
using Cadl.Rest;
using Cadl.Versioning;
using Azure.ResourceManager;
using Azure.ResourceManager.Radius;
using OpenAPI;

namespace Applications.Datastores;
@doc("Mongo database portable resource")
model MongoDatabaseResource is TrackedResource<MongoDatabaseProperties> {
  @doc("The name of the Mongo database portable resource")
  @maxLength(63)
  @pattern("^[A-Za-z]([-A-Za-z0-9]*[A-Za-z0-9])?$")
  @path
  @segment("mongoDatabases")
  @key("mongoDatabaseName")
  @extension("x-ms-parameter-location", "method")
  name: string;
}

@doc("The secret values for the given Mongo database resource")
model MongoDatabaseListSecretsResult is MongoDatabaseSecrets;

@doc("The secret values for the given Mongo database resource")
model MongoDatabaseSecrets {
  @doc("Password to use when connecting to the target Mongo database")
  password?: string;

  @doc("Connection string used to connect to the target Mongo database")
  connectionString?: string;
}

@doc("Mongo database portable resource properties")
model MongoDatabaseProperties extends BasicResourceProperties{
  @doc("Provisioning state of the Mongo database portable resource at the time the operation was called")
  @visibility("read")
  provisioningState?: ProvisioningState;

  @doc("Secret values provided for the resource")
  secrets?: MongoDatabaseSecrets;

  @doc("Host name of the target Mongo database")
  host?: string;

  @doc("Port value of the target Mongo database")
  port?: int32;

  @doc("Database name of the target Mongo database")
  database?: string;

  @doc("The recipe used to automatically deploy underlying infrastructure for the Mongo database portable resource")
  recipe?: Recipe;

  @doc("List of the resource IDs that support the Mongo database resource")
  resources?: ResourceReference[];

  @doc("Specifies how the underlying service/resource is provisioned and managed.")
  resourceProvisioning?: ResourceProvisioning;

  @doc("Username to use when connecting to the target Mongo database")
  username?: string;
}

#suppress "@azure-tools/cadl-azure-resource-manager/arm-resource-operation-outside-interface" "This is an interface template"
@doc("Lists secrets values for the specified Mongo database resource")
@post
@route("/{rootScope}/providers/Applications.Datastores/mongoDatabases/{mongoDatabaseName}/listSecrets")
op MongoDatabases_listSecrets(...ApiVersionParameter,
  ...RootScopeParam,
  @doc("The name of the Mongo database portable resource")
  @maxLength(63)
  @pattern("^[A-Za-z]([-A-Za-z0-9]*[A-Za-z0-9])?$")
  @path
  @extension("x-ms-parameter-location", "method")
  mongoDatabaseName: string): ArmResponse<MongoDatabaseListSecretsResult>
   | ErrorResponse;

@armResourceOperations
interface MongoDatabases
  extends Radius.ResourceOperationWithAsyncDelete<
      MongoDatabaseResource,
      MongoDatabaseProperties,
      RootScopeParam
    > {}