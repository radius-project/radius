/*
Copyright 2023 The Radius Authors.
Licensed under the Apache License, Version 2.0 (the "License");
you may not use this file except in compliance with the License.
You may obtain a copy of the License at

    http://www.apache.org/licenses/LICENSE-2.0
    
Unless required by applicable law or agreed to in writing, software
distributed under the License is distributed on an "AS IS" BASIS,
WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
See the License for the specific language governing permissions and
limitations under the License.
*/

import "@cadl-lang/rest";
import "@cadl-lang/versioning";
import "@azure-tools/cadl-azure-core";
import "@azure-tools/cadl-azure-resource-manager";
import "@azure-tools/cadl-providerhub";
import "@cadl-lang/openapi";

// These files allow the use of {rootScope} in the generated paths with a custom resourceOperations object
import "../customRootScope.cadl";
import "../aksrootscope.cadl";

import "./global.cadl";

using Cadl.Http;
using Cadl.Rest;
using Cadl.Versioning;
using Azure.ResourceManager;
using Azure.ResourceManager.Radius;
using OpenAPI;

@service({
  title: "Applications.Link Management APIs",
  version: "2022-03-15-privatepreview",
})
@doc("REST APIs for Applications.Link/daprPubSubBrokers")
@versionedDependency(Azure.ResourceManager.Versions.v1_0_Preview_1)
namespace Applications.Link;

@doc("DaprPubSubBroker Properties for Mode Resource")
model ResourceDaprPubSubProperties extends DaprPubSubBrokerProperties{
  @doc("How to build the DaprPubSubBroker link. Options are to build automatically via 'recipe' or 'resource', or build manually via 'values'. Selection determines which set of fields to additionally require.")
  mode: "resource";

  @doc("Metadata for the pub sub resource. This should match the values specified in Dapr component spec")
  metadata?: object;

  @doc("Dapr PubSub type. These strings match the format used by Dapr Kubernetes configuration format.")
  type?: string;

  @doc("Dapr component version")
  version?: string;

  @doc("PubSub resource")
  resource: string;
}

@doc("DaprPubSubBroker Properties for Mode Values")
model ValuesDaprPubSubProperties extends DaprPubSubBrokerProperties{
  @doc("How to build the DaprPubSubBroker link. Options are to build automatically via 'recipe' or 'resource', or build manually via 'values'. Selection determines which set of fields to additionally require.")
  mode: "values";

  @doc("Metadata for the pub sub resource. This should match the values specified in Dapr component spec")
  metadata: object;

  @doc("Dapr PubSub type. These strings match the format used by Dapr Kubernetes configuration format.")
  type: string;

  @doc("Dapr component version")
  version: string;
}

@doc("DaprPubSubBroker Properties for Mode Recipe")
model RecipeDaprPubSubProperties extends DaprPubSubBrokerProperties{
  @doc("How to build the DaprPubSubBroker link. Options are to build automatically via 'recipe' or 'resource', or build manually via 'values'. Selection determines which set of fields to additionally require.")
  mode: "recipe";

  @doc("Metadata for the pub sub resource. This should match the values specified in Dapr component spec")
  metadata?: object;

  @doc("Dapr PubSub type. These strings match the format used by Dapr Kubernetes configuration format.")
  type?: string;

  @doc("Dapr component version")
  version?: string;

  @doc("The recipe used to automatically deploy underlying infrastructure for the daprPubSub link")
  recipe: Recipe;
}
@doc("DaprPubSubBroker link properties")
@discriminator("mode")
model DaprPubSubBrokerProperties extends BasicDaprResourceProperties {
  @doc("Provisioning state of the daprPubSubBroker link at the time the operation was called")
  @visibility("read")
  provisioningState?: ProvisioningState;

  @doc("Topic name of the Azure ServiceBus resource")
  topic?: string;
}
@doc("DaprPubSubBroker link")
model DaprPubSubBrokerResource is TrackedResource<DaprPubSubBrokerProperties> {
  @doc("DaprPubSubBroker name")
  @maxLength(63)
  @pattern("^[A-Za-z]([-A-Za-z0-9\\.]*[A-Za-z0-9\\.])?$")
  @key("daprPubSubBrokerName")
  @path
  @extension("x-ms-parameter-location", "method")
  @segment("daprPubSubBrokers")
  name: string;
}

@armResourceOperations
interface DaprPubSubBroker
  extends Radius.ResourceOperationWithSyncDelete<
      DaprPubSubBrokerResource,
      DaprPubSubBrokerProperties,
      RootScopeParam
    > {}
