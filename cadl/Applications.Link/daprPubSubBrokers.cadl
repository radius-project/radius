/*
Copyright 2023 The Radius Authors.

Licensed under the Apache License, Version 2.0 (the "License");
you may not use this file except in compliance with the License.
You may obtain a copy of the License at

    http://www.apache.org/licenses/LICENSE-2.0
    
Unless required by applicable law or agreed to in writing, software
distributed under the License is distributed on an "AS IS" BASIS,
WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
See the License for the specific language governing permissions and
limitations under the License.
*/

import "@cadl-lang/rest";
import "@cadl-lang/versioning";
import "@azure-tools/cadl-azure-core";
import "@azure-tools/cadl-azure-resource-manager";
import "@azure-tools/cadl-providerhub";
import "@cadl-lang/openapi";

// These files allow the use of {rootScope} in the generated paths with a custom resourceOperations object
import "../customRootScope.cadl";
import "../aksrootscope.cadl";

import "./global.cadl";

using Cadl.Http;
using Cadl.Rest;
using Cadl.Versioning;
using Azure.ResourceManager;
using Azure.ResourceManager.Radius;
using OpenAPI;

@service({
  title: "Applications.Link Management APIs",
  version: "2022-03-15-privatepreview",
})
@doc("REST APIs for Applications.Link DaprPubSubBroker Resource")
@versionedDependency(Azure.ResourceManager.Versions.v1_0_Preview_1)
namespace Applications.Link;

@doc("DaprPubSubBroker link properties")
model DaprPubSubBrokerProperties extends BasicDaprResourceProperties {
  @doc("Provisioning state of the daprPubSubBroker resource at the time the operation was called")
  @visibility("read")
  provisioningState?: ProvisioningState;

  @doc("Specifies how the underlying service/resource is provisioned and managed.")
  resourceProvisioning?: ResourceProvisioning;

  @doc("Metadata for the daprPubSubBroker resource. This should match the values specified in Dapr component spec")
  metadata?: object;

  @doc("The recipe used to automatically deploy underlying infrastructure for the daprPubSubBroker link")
  recipe?: Recipe;

  @doc("A collection of references to resources associated with the daprPubSubBroker")
  resources?: ResourceReference[];

  @doc("DaprPubSubBroker type. These strings match the format used by Dapr Kubernetes configuration format.")
  type?: string;

  @doc("Dapr component version")
  version?: string;
}

@doc("DaprPubSubBroker link")
model DaprPubSubBrokerResource is TrackedResource<DaprPubSubBrokerProperties> {
  @doc("DaprPubSubBroker name")
  @maxLength(63)
  @pattern("^[A-Za-z]([-A-Za-z0-9\\.]*[A-Za-z0-9\\.])?$")
  @key("daprPubSubBrokerName")
  @path
  @extension("x-ms-parameter-location", "method")
  @segment("daprPubSubBrokers")
  name: string;
}

@armResourceOperations
interface DaprPubSubBroker
  extends Radius.ResourceOperationWithAsyncDelete<
      DaprPubSubBrokerResource,
      DaprPubSubBrokerProperties,
      RootScopeParam
    > {}
