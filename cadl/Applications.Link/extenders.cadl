// ------------------------------------------------------------
// Copyright (c) Microsoft Corporation.
// Licensed under the MIT License.
// ------------------------------------------------------------

import "@cadl-lang/rest";
import "@cadl-lang/versioning";
import "@azure-tools/cadl-providerhub";
import "@azure-tools/cadl-azure-core";
import "@azure-tools/cadl-azure-resource-manager";
import "@cadl-lang/openapi";

// These files allow the use of {rootScope} in the generated paths with a custom resourceOperations object
import "../customRootScope.cadl";
import "../aksrootscope.cadl";

import "./global.cadl";

using Cadl.Http;
using Cadl.Rest;
using Cadl.Versioning;
using Azure.ResourceManager;
using Azure.ResourceManager.Radius;
using OpenAPI;

@service({
  title: "Applications.Link Management APIs",
  version: "2022-03-15-privatepreview",
})
@doc("REST API for Applications.Link Extender Resource")
@versionedDependency(Azure.ResourceManager.Versions.v1_0_Preview_1)
namespace Applications.Link;

@doc("Extender link")
model ExtenderResource is TrackedResource<ExtenderProperties> {
  @doc("Extender name")
  @maxLength(63)
  @pattern("^[a-z]([-a-z0-9]*[a-z0-9])?$")
  @key("extenderName")
  @path
  @segment("extenders")
  @extension("x-ms-parameter-location", "method")
  name: string;
}
@doc("The secret values for the given Extender resource")
model ExtenderListSecretsResult is ExtenderSecrets;

@doc("The secret values for the given Extender resource")
model ExtenderSecrets {}

@doc("Extender link properties")
model ExtenderProperties extends BasicResourceProperties {
  @doc("Provisioning state of the extender link at the time the operation was called")
  @visibility("read")
  provisioningState?: ProvisioningState;

  @doc("The secrets value for the resource")
  secrets?: ExtenderSecrets;
}

#suppress "@azure-tools/cadl-azure-resource-manager/arm-resource-operation-outside-interface" "This is an interface template"
@doc("Lists secrets values for the specified Extender resource")
@route("/{rootScope}/providers/Applications.Link/extenders/{extenderName}/listSecrets")
op Extenders_listSecrets(
  ...ApiVersionParameter,
  ...RootScopeParam,
  @doc("The name of the Extender link resource")
  @maxLength(63)
  @pattern("^[a-z]([-a-z0-9]*[a-z0-9])?$")
  @path
  @extension("x-ms-parameter-location", "method")
  extenderName: string): ArmResponse<ExtenderListSecretsResult> | ErrorResponse;

@armResourceOperations
interface Extenders
  extends Radius.RootScopeResourceOperations<
      ExtenderResource,
      ExtenderProperties,
      RootScopeParam
    > {}
