/*
Copyright 2023 The Radius Authors.

Licensed under the Apache License, Version 2.0 (the "License");
you may not use this file except in compliance with the License.
You may obtain a copy of the License at

    http://www.apache.org/licenses/LICENSE-2.0
    
Unless required by applicable law or agreed to in writing, software
distributed under the License is distributed on an "AS IS" BASIS,
WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
See the License for the specific language governing permissions and
limitations under the License.
*/

import "@cadl-lang/rest";
import "@cadl-lang/versioning";
import "@azure-tools/cadl-azure-core";
import "@azure-tools/cadl-azure-resource-manager";
import "@azure-tools/cadl-providerhub";
import "@cadl-lang/openapi";

// These files allow the use of {rootScope} in the generated paths with a custom resourceOperations object
import "../customRootScope.cadl";
import "../aksrootscope.cadl";

import "./global.cadl";

using Cadl.Http;
using Cadl.Rest;
using Cadl.Versioning;
using Azure.ResourceManager;
using Azure.ResourceManager.Radius;
using OpenAPI;

@service({
  title: "Applications.Link Management APIs",
  version: "2022-03-15-privatepreview",
})
@doc("REST APIs for Applications.Link/rabbitMQMessageQueues")
@versionedDependency(Azure.ResourceManager.Versions.v1_0_Preview_1)
namespace Applications.Link;

#suppress "@azure-tools/cadl-azure-core/casing-style" "The names of Model types must use PascalCase"
@doc("RabbitMQMessageQueue link")
model RabbitMQMessageQueueResource
  is TrackedResource<RabbitMQMessageQueueProperties> {
  @doc("The name of the RabbitMQMessageQueue link resource")
  @maxLength(63)
  @pattern("^[A-Za-z]([-A-Za-z0-9]*[A-Za-z0-9])?$")
  @key("rabbitMQMessageQueueName")
  @path
  @segment("rabbitMQMessageQueues")
  name: string;
}

#suppress "@azure-tools/cadl-azure-core/casing-style" "The names of Model types must use PascalCase"
@doc("The secret values for the given RabbitMQMessageQueue resource")
model RabbitMQListSecretsResult is RabbitMQSecrets;

#suppress "@azure-tools/cadl-azure-core/casing-style" "The names of Model types must use PascalCase"
@doc("The secret values for the given RabbitMQMessageQueue resource")
model RabbitMQSecrets {
  @doc("The password used to connect to the RabbitMQ instance")
  password?: string;

  @doc("The connection URI of the RabbitMQ instance. Generated automatically from host, port, SSL, username, password, and vhost. Can be overridden with a custom value")
  uri?: string;
}
#suppress "@azure-tools/cadl-azure-core/casing-style" "The names of Model types must use PascalCase"
@doc("RabbitMQMessageQueue link properties")
model RabbitMQMessageQueueProperties extends BasicResourceProperties {
  @doc("Provisioning state of the rabbitMQ message queue link at the time the operation was called")
  @visibility("read")
  provisioningState?: ProvisioningState;

  @doc("Secrets provided by resources,")
  secrets?: RabbitMQSecrets;

  @doc("The name of the queue")
  queue?: string;

  @doc("The hostname of the RabbitMQ instance")
  host?: string;

  @doc("The port of the RabbitMQ instance. Defaults to 5672")
  port?: int32;

  @doc("The RabbitMQ virtual host (vHost) the client will connect to. Defaults to no vHost.")
  vHost?: string;

  @doc("The username to use when connecting to the RabbitMQ instance")
  username?: string;

  @doc("List of the resource IDs that support the rabbitMQ resource")
  resources?: ResourceReference[];

  @doc("Specifies how the underlying service/resource is provisioned and managed.")
  resourceProvisioning?: ResourceProvisioning;

  @doc("The recipe used to automatically deploy underlying infrastructure for the rabbitMQ link")
  recipe?: Recipe;

  @doc("Specifies whether to use SSL when connecting to the RabbitMQ instance")
  tls?: boolean;

}

#suppress "@azure-tools/cadl-azure-core/casing-style" "The names of Model types must use camelCase"
#suppress "@azure-tools/cadl-azure-resource-manager/arm-resource-operation-outside-interface" "This is an interface template"
@doc("Lists secrets values for the specified RabbitMQMessageQueue resource")
@post
@route("/{rootScope}/providers/Applications.Link/rabbitMQMessageQueues/{rabbitMQMessageQueueName}/listSecrets")
op RabbitMQMessageQueues_listSecrets(
  ...ApiVersionParameter,
  ...RootScopeParam,
  @doc("The name of the RabbitMQMessageQueue link resource")
  @maxLength(63)
  @pattern("^[A-Za-z]([-A-Za-z0-9]*[A-Za-z0-9])?$")
  @path
  @extension("x-ms-parameter-location", "method")
  rabbitMQMessageQueueName: string
): ArmResponse<RabbitMQListSecretsResult> | ErrorResponse;

#suppress "@azure-tools/cadl-azure-core/casing-style" "The names of Interface types must use PascalCase"
@armResourceOperations
interface RabbitMQMessageQueues
  extends Radius.ResourceOperationWithSyncDelete<
      RabbitMQMessageQueueResource,
      RabbitMQMessageQueueProperties,
      RootScopeParam
    > {}
