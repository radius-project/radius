// ------------------------------------------------------------
// Copyright (c) Microsoft Corporation.
// Licensed under the MIT License.
// ------------------------------------------------------------

import "@cadl-lang/rest";
import "@cadl-lang/versioning";
import "@azure-tools/cadl-azure-core";
import "@azure-tools/cadl-azure-resource-manager";
import "@azure-tools/cadl-providerhub";
import "@cadl-lang/openapi";

// These files allow the use of {rootScope} in the generated paths with a custom resourceOperations object
import "../customRootScope.cadl";
import "../aksrootscope.cadl";

import "./global.cadl";

using Cadl.Http;
using Cadl.Rest;
using Cadl.Versioning;
using Azure.ResourceManager;
using Azure.ResourceManager.Radius;
using OpenAPI;

@service({
  title: "Applications.Link Management APIs",
  version: "2022-03-15-privatepreview",
})
@doc("REST API for Applications.Link RabbitmqMessageQueue Resource")
@versionedDependency(Azure.ResourceManager.Versions.v1_0_Preview_1)
namespace Applications.Link;

enum RabbitmqMode {
  values,
  recipe,
}
@doc("RabbitmqMessageQueue link")
model RabbitmqMessageQueueResource
  is TrackedResource<RabbitmqMessageQueueProperties> {
  @doc("The name of the RabbitmqMessageQueue link resource")
  @maxLength(63)
  @pattern("^[a-z]([-a-z0-9]*[a-z0-9])?$")
  @key("rabbitmqMessageQueueName")
  @path
  @segment("rabbitmqMessageQueues")
  name: string;
}

@doc("The secret values for the given RabbitmqMessageQueue resource")
model RabbitmqListSecretsResult is RabbitmqSecrets;

@doc("The secret values for the given RabbitmqMessageQueue resource")
model RabbitmqSecrets {
  @doc("The connection string used to connect to this Rabbitmq instance")
  connectionString?: string;
}
@doc("MongoDatabase Properties for Mode Values")
model ValuesRabbitmqMessageQueueProperties extends RabbitmqMessageQueueProperties {
  @doc("How to build the Mongo database link. Options are to build automatically via 'recipe' or 'resource', or build manually via 'values'. Selection determines which set of fields to additionally require.")
  mode: RabbitmqMode.values;

  @doc("The name of the queue")
  queue: string;
}
@doc("MongoDatabase Properties for Mode Recipe")
model RecipeRabbitmqMessageQueueProperties extends RabbitmqMessageQueueProperties {
  @doc("How to build the Mongo database link. Options are to build automatically via 'recipe' or 'resource', or build manually via 'values'. Selection determines which set of fields to additionally require.")
  mode: RabbitmqMode.recipe;

  @doc("The name of the queue")
  queue?: string;

  @doc("The recipe used to automatically deploy underlying infrastructure for the mongodatabase link")
  recipe: Recipe;
}
@doc("RabbitmqMessageQueue link response properties")
@discriminator("mode")
model RabbitmqMessageQueueProperties extends BasicResourceProperties {
  @doc("Provisioning state of the rabbitmq message queue link at the time the operation was called")
  @visibility("read")
  provisioningState?: ProvisioningState;

  @doc("Secrets provided by resources,")
  secrets?: RabbitmqSecrets;
}

#suppress "@azure-tools/cadl-azure-resource-manager/arm-resource-operation-outside-interface" "This is an interface template"
@doc("Lists secrets values for the specified RabbitmqMessageQueue resource")
@route("/{rootScope}/providers/Applications.Link/rabbitmqMessageQueues/{rabbitmqMessageQueueName}/listSecrets")
op RabbitmqMessageQueues_listSecrets(
  ...ApiVersionParameter,
  ...RootScopeParam,
  @doc("The name of the RabbitmqMessageQueue link resource")
  @maxLength(63)
  @pattern("^[a-z]([-a-z0-9]*[a-z0-9])?$")
  @path
  @extension("x-ms-parameter-location", "method")
  rabbitmqMessageQueueName: string
): ArmResponse<RabbitmqListSecretsResult> | ErrorResponse;

@armResourceOperations
interface RabbitmqMessageQueues
  extends Radius.RootScopeResourceOperations<
      RabbitmqMessageQueueResource,
      RabbitmqMessageQueueProperties,
      RootScopeParam
    > {}
