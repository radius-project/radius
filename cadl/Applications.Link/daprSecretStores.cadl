/*
Copyright 2023 The Radius Authors.
Licensed under the Apache License, Version 2.0 (the "License");
you may not use this file except in compliance with the License.
You may obtain a copy of the License at
    http://www.apache.org/licenses/LICENSE-2.0
Unless required by applicable law or agreed to in writing, software
distributed under the License is distributed on an "AS IS" BASIS,
WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
See the License for the specific language governing permissions and
limitations under the License.
*/

import "@cadl-lang/rest";
import "@cadl-lang/versioning";
import "@azure-tools/cadl-azure-core";
import "@azure-tools/cadl-azure-resource-manager";
import "@azure-tools/cadl-providerhub";
import "@cadl-lang/openapi";

// These files allow the use of {rootScope} in the generated paths with a custom resourceOperations object
import "../customRootScope.cadl";
import "../aksrootscope.cadl";

import "./global.cadl";

using Cadl.Http;
using Cadl.Rest;
using Cadl.Versioning;
using Azure.ResourceManager;
using Azure.ResourceManager.Radius;
using OpenAPI;

@service({
  title: "Applications.Link Management APIs",
  version: "2022-03-15-privatepreview",
})
@doc("REST APIs for Applications.Link/daprSecretStores")
@versionedDependency(Azure.ResourceManager.Versions.v1_0_Preview_1)
namespace Applications.Link;

@doc("DaprSecretStore Properties for Mode Values")
model ValuesDaprSecretStoreProperties extends DaprSecretStoreProperties {
  @doc("How to build the DaprSecretStore link. Options are to build automatically via 'recipe' or 'resource', or build manually via 'values'. Selection determines which set of fields to additionally require.")
  mode: "values";

  @doc("Dapr Secret Store type. These strings match the types defined in Dapr Component format: https://docs.dapr.io/reference/components-reference/supported-secret-stores/")
  type: string;

  @doc("Dapr component version")
  version: string;

  @doc("Metadata for the Secret Store resource. This should match the values specified in Dapr component spec")
  metadata: object;
}

@doc("DaprSecretStore Properties for Mode Recipe")
model RecipeDaprSecretStoreProperties extends DaprSecretStoreProperties {
  @doc("How to build the DaprSecretStore link. Options are to build automatically via 'recipe' or 'resource', or build manually via 'values'. Selection determines which set of fields to additionally require.")
  mode: "recipe";

  @doc("Dapr Secret Store type. These strings match the types defined in Dapr Component format: https://docs.dapr.io/reference/components-reference/supported-secret-stores/")
  type?: string;

  @doc("Dapr component version")
  version?: string;

  @doc("Metadata for the Secret Store resource. This should match the values specified in Dapr component spec")
  metadata?: object;

  @doc("The recipe used to automatically deploy underlying infrastructure for the daprSecretStore link")
  recipe: Recipe;
}

@doc("DaprSecretStore link properties")
@discriminator("mode")
model DaprSecretStoreProperties extends BasicDaprResourceProperties {
  @doc("Provisioning state of the dapr secret store link at the time the operation was called")
  @visibility("read")
  provisioningState?: ProvisioningState;
}

@doc("DaprSecretStore link")
model DaprSecretStoreResource is TrackedResource<DaprSecretStoreProperties> {
  @doc("DaprSecretStore name")
  @maxLength(63)
  @pattern("^[A-Za-z]([-A-Za-z0-9\\.]*[A-Za-z0-9\\.])?$")
  @key("daprSecretStoreName")
  @path
  @segment("daprSecretStores")
  @extension("x-ms-parameter-location", "method")
  name: string;
}

@armResourceOperations
interface DaprSecretStore
  extends Radius.ResourceOperationWithSyncDelete<
      DaprSecretStoreResource,
      DaprSecretStoreProperties,
      RootScopeParam
    > {}
