// ------------------------------------------------------------
// Copyright (c) Microsoft Corporation.
// Licensed under the MIT License.
// ------------------------------------------------------------

import "@cadl-lang/rest";
import "@cadl-lang/versioning";
import "@azure-tools/cadl-azure-core";
import "@azure-tools/cadl-azure-resource-manager";
import "@azure-tools/cadl-providerhub";

// These files allow the use of {rootScope} in the generated paths with a custom resourceOperations object
import "../customRootScope.cadl";
import "../aksrootscope.cadl";

import "./global.cadl";

using Cadl.Http;
using Cadl.Rest;
using Cadl.Versioning;
using Azure.ResourceManager;
using Azure.ResourceManager.Radius;

@armNamespace
@serviceTitle("Applications.Link Management APIs")
@doc("REST API for Applications.Link DaprSecretStore Resource")
@versionedDependency(Azure.ResourceManager.Versions.v1_0_Preview_1)
namespace Applications.Link;

@doc("DaprSecretStore properties")
model DaprSecretStoreProperties extends BasicResourceProperties {
  @doc("Provisioning state of the dapr secret store link at the time the operation was called")
  @visibility("read")
  provisioningState?: ProvisioningState;

  @doc("Fully qualified resource ID for the application that the link is consumed by")
  application?: Application;

  @doc("Fully qualified resource ID for the environment that the link is linked to")
  environment: string;

  @doc("Radius kind for Dapr Secret Store")
  kind: "generic";

  @doc("Dapr Secret Store type. These strings match the types defined in Dapr Component format: https://docs.dapr.io/reference/components-reference/supported-secret-stores/")
  type: string;

  @doc("Dapr component version")
  version: string;

  @doc("Metadata for the Secret Store resource. This should match the values specified in Dapr component spec")
  metadata: object;

  @doc("The name of the Dapr component object. Use this value in your code when interacting with the Dapr client to use the secret store.")
  @visibility("read")
  componentName?: string;
}

model DaprSecretStoreResource is TrackedResource<DaprSecretStoreProperties> {
  @doc("DaprSecretStore name")
  @maxLength(63)
  @pattern("^[a-z]([-a-z0-9]*[a-z0-9])?$")
  @key("daprSecretStoreName")
  @path
  @segment("daprSecretStores")
  name: string;
}

@armResourceOperations
interface DaprSecretStore
  extends Radius.RootScopeResourceOperations<
      DaprSecretStoreResource,
      DaprSecretStoreProperties,
      RootScopeParam
    > {}
