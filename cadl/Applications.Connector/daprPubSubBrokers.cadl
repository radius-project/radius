// ------------------------------------------------------------
// Copyright (c) Microsoft Corporation.
// Licensed under the MIT License.
// ------------------------------------------------------------

import "@cadl-lang/rest";
import "@cadl-lang/versioning";
import "@azure-tools/cadl-azure-core";
import "@azure-tools/cadl-azure-resource-manager";
import "@azure-tools/cadl-providerhub";

// These files allow the use of {rootScope} in the generated paths with a custom resourceOperations object
import "../customRootScope.cadl";
import "../aksrootscope.cadl";

import "./global.cadl";

using Cadl.Http;
using Cadl.Rest;
using Cadl.Versioning;
using Azure.ResourceManager;
using Azure.ResourceManager.Radius;

@armNamespace
@serviceTitle("Applications.Connector Management APIs")
@doc("REST API for Applications.Connector DaprPubSubBroker Resource")
@versionedDependency(Azure.ResourceManager.Versions.v1_0_Preview_1)
namespace Applications.Connector;

@doc("DaprPubSubBroker properties")
@discriminator("kind")
model DaprPubSubBrokerProperties extends BasicResourceProperties {
  @doc("Provisioning state of the daprPubSubBroker connector at the time the operation was called")
  @visibility("read")
  provisioningState?: ProvisioningState;

  @doc("Fully qualified resource ID for the application that the connector is consumed by")
  application?: Application;

  @doc("The resource id of the environment linked to the daprPubSubBroker connector")
  environment: string;

  @doc("Topic name of the Azure ServiceBus resource")
  topic?: string;
}

@doc("DaprPubSubAzureServiceBus properties")
model DaprPubSubAzureServiceBusResourceProperties
  extends DaprPubSubBrokerProperties {
  @doc("The DaprPubSubProperties kind")
  kind: "pubsub.azure.servicebus";

  @doc("PubSub resource")
  resource: string;
}

@doc("DaprPubSubBroker generic resource properties")
model DaprPubSubGenericResourceProperties extends DaprPubSubBrokerProperties {
  @doc("The DaprPubSubProperties kind")
  kind: "generic";

  @doc("Metadata for the pub sub resource. This should match the values specified in Dapr component spec")
  metadata: object;

  @doc("Dapr PubSub type. These strings match the format used by Dapr Kubernetes configuration format.")
  type: string;

  @doc("Dapr component version")
  version: string;
}

model DaprPubSubBrokerResource is TrackedResource<DaprPubSubBrokerProperties> {
  @doc("DaprPubSubBroker name")
  @maxLength(63)
  @pattern("^[a-z]([-a-z0-9]*[a-z0-9])?$")
  @key("daprPubSubBrokerName")
  @path
  @segment("daprPubSubBrokers")
  name: string;
}

@armResourceOperations
interface DaprPubSubBroker
  extends Radius.RootScopeResourceOperations<
      DaprPubSubBrokerResource,
      DaprPubSubBrokerProperties,
      RootScopeParam
    > {}
