// ------------------------------------------------------------
// Copyright (c) Microsoft Corporation.
// Licensed under the MIT License.
// ------------------------------------------------------------

import "@cadl-lang/rest";
import "@cadl-lang/versioning";
import "@azure-tools/cadl-azure-core";
import "@azure-tools/cadl-azure-resource-manager";
import "@azure-tools/cadl-providerhub";

// These files allow the use of {rootScope} in the generated paths with a custom resourceOperations object
import "../customRootScope.cadl";
import "../aksrootscope.cadl";

import "./global.cadl";

using Cadl.Http;
using Cadl.Rest;
using Cadl.Versioning;
using Azure.ResourceManager;
using Azure.ResourceManager.Radius;

@armNamespace
@serviceTitle("Applications.Link Management APIs")
@doc("REST API for Applications.Link SQLDatabase Resource")
@versionedDependency(Azure.ResourceManager.Versions.v1_0_Preview_1)
namespace Applications.Link;

@doc("SQLDatabase properties")
model SQLDatabaseProperties extends BasicResourceProperties {
  @doc("Provisioning state of the SQL database connector at the time the operation was called")
  @visibility("read")
  provisioningState?: ProvisioningState;

  @doc("Fully qualified resource ID for the environment that the connector is linked to")
  environment: string;

  @doc("Fully qualified resource ID for the application that the connector is consumed by")
  application?: Application;

  @doc("Fully qualified resource ID of a supported resource with SQL API to use for this connector")
  resource?: string;

  @doc("The name of the SQL database.")
  database?: string;

  @doc("Metadata for the Secret Store resource. This should match the values specified in Dapr component spec")
  server?: string;
}

model SQLDatabaseResource is TrackedResource<SQLDatabaseProperties> {
  @doc("SQLDatabase name")
  @maxLength(63)
  @pattern("^[a-z]([-a-z0-9]*[a-z0-9])?$")
  @key("SQLDatabaseName")
  @path
  @segment("SQLDatabases")
  name: string;
}

@armResourceOperations
interface SQLDatabase
  extends Radius.RootScopeResourceOperations<
      SQLDatabaseResource,
      SQLDatabaseProperties,
      RootScopeParam
    > {}
